// Copyright (c) 2024-2025 The Khronos Group Inc.
// Copyright (c) 2024-2025 Valve Corporation
// Copyright (c) 2024-2025 LunarG, Inc.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

// NOTE: This file doesn't contain any entrypoints and should be compiled with the --no-link option for glslang

#version 450
#extension GL_GOOGLE_include_directive : enable
#include "common_descriptor_sets.h"

layout(buffer_reference, buffer_reference_align = 8, std430) buffer DescriptorSetType {
    // struct glsl::DescriptorState {
    //   x: id
    //   y: extra data depending on the descriptor type
    // }
    uvec2 data[];
};

layout(buffer_reference, buffer_reference_align = 8, std430) buffer UnusedInitializedStatus;

layout(set = kInstDefaultDescriptorSet, binding = kBindingInstDescriptorIndexingOOB, std430) buffer DescriptorStateSSBO {
    UnusedInitializedStatus unused_initialized_status;
    DescriptorSetType descriptor_set_types[kDebugInputBindlessMaxDescSets];
} gpuav;

// Matches the vvl::DescriptorClass::GeneralBuffer
//
// If we have a shader like
//    layout(set=0, binding=0) buffer foo {
//        uint a;
//        uint b;
//    }
//    void main() {
//        b = 0;
//    }
//
// We might have only bound 4 bytes to the buffer, so 'b' is OOB and this function will detect it
bool inst_descriptor_class_general_buffer(const uint inst_num, const uvec4 stage_info,
                        const uint desc_set, const uint binding, const uint desc_index, const uint byte_offset, const uint binding_layout_offset) {
    uint error = 0u;
    uint param_0 = byte_offset;
    uint param_1 = 0;
    do {
        DescriptorSetType descriptor_set_type = gpuav.descriptor_set_types[desc_set];

        uint binding_layout_index = binding_layout_offset + desc_index;

        // check that the offset is in bounds
        uint resource_size = descriptor_set_type.data[binding_layout_index].y;
        if (byte_offset >= resource_size) {
            error = kErrorSubCodeDescriptorClassGeneralBufferBounds;
            param_1 = resource_size;
            break;
        }
    } while (false);

    if (0u != error) {

        const uint cmd_id = inst_cmd_resource_index_buffer.index[0];
        const uint cmd_errors_count = atomicAdd(inst_cmd_errors_count_buffer.errors_count[cmd_id], 1);
        const bool max_cmd_errors_count_reached = cmd_errors_count >= kMaxErrorsPerCmd;

        if (max_cmd_errors_count_reached) return false;

        uint write_pos = atomicAdd(inst_errors_buffer.written_count, kErrorRecordSize);
        const bool errors_buffer_not_filled = (write_pos + kErrorRecordSize) <= uint(inst_errors_buffer.data.length());

        if (errors_buffer_not_filled) {
            inst_errors_buffer.data[write_pos + kHeaderErrorRecordSizeOffset] = kErrorRecordSize;
            inst_errors_buffer.data[write_pos + kHeaderShaderIdOffset] = kLinkShaderId;
            inst_errors_buffer.data[write_pos + kHeaderInstructionIdOffset] = inst_num;
            inst_errors_buffer.data[write_pos + kHeaderStageIdOffset] = stage_info.x;
            inst_errors_buffer.data[write_pos + kHeaderStageInfoOffset_0] = stage_info.y;
            inst_errors_buffer.data[write_pos + kHeaderStageInfoOffset_1] = stage_info.z;
            inst_errors_buffer.data[write_pos + kHeaderStageInfoOffset_2] = stage_info.w;

            inst_errors_buffer.data[write_pos + kHeaderErrorGroupOffset] = kErrorGroupInstDescriptorClass;
            inst_errors_buffer.data[write_pos + kHeaderErrorSubCodeOffset] = error;

            inst_errors_buffer.data[write_pos + kHeaderActionIdOffset] = inst_action_index_buffer.index[0];
            inst_errors_buffer.data[write_pos + kHeaderCommandResourceIdOffset] = inst_cmd_resource_index_buffer.index[0];

            inst_errors_buffer.data[write_pos + kInstDescriptorClassDescSetOffset] = desc_set;
            inst_errors_buffer.data[write_pos + kInstDescriptorClassDescBindingOffset] = binding;
            inst_errors_buffer.data[write_pos + kInstDescriptorClassDescIndexOffset] = desc_index;
            inst_errors_buffer.data[write_pos + kInstDescriptorClassParamOffset_0] = param_0;
            inst_errors_buffer.data[write_pos + kInstDescriptorClassParamOffset_1] = param_1;
        }
        return false;
    }
    return true;
}