// Copyright (c) 2021-2025 The Khronos Group Inc.
// Copyright (c) 2021-2025 Valve Corporation
// Copyright (c) 2021-2025 LunarG, Inc.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

#version 450
#extension GL_GOOGLE_include_directive : enable

#include "common.h"
#include "draw_push_data.h"

layout (push_constant)
uniform UniformInfo {
    DrawMeshPushData pc;
};

/*
struct VkDrawMeshTasksIndirectCommandEXT {
    uint32_t    groupCountX;
    uint32_t    groupCountY;
    uint32_t    groupCountZ;
};
*/

const uint kGroupCountX = 0;
const uint kGroupCountY = 1;
const uint kGroupCountZ = 2;

layout(set = kDiagPerCmdDescriptorSet, binding = kPreDrawBinding_IndirectBuffer)
readonly buffer DrawBuffer {
    uint draw_buffer[];
};

layout(set = kDiagPerCmdDescriptorSet, binding = kPreDrawBinding_CountBuffer) 
readonly buffer CountBuffer {
    uint count_buffer[];
};

layout(local_size_x = 1, local_size_y = 1, local_size_z = 1) in;
void main() {
    const uint draw_i = gl_GlobalInvocationID.x;

    if ((pc.flags & kDrawMeshFlags_DrawCountFromBuffer) != 0) {
        // Need to clamp draw count value stored in count buffer to the maxDrawCount
        // that was specified at vkCmdDrawIndexedIndirect time.
        // pc.cpu_draw_count is used to store maxDrawCount
        const uint draw_count = min(count_buffer[pc.count_buffer_dwords_offset], pc.cpu_draw_count);
        if (draw_i >= draw_count) return;
    }
    const uint draw_mesh_task_indirect_cmd_i = draw_i * pc.draw_cmds_stride_dwords + pc.draw_buffer_dwords_offset;
    const uint x = draw_buffer[ draw_mesh_task_indirect_cmd_i + kGroupCountX ];
    const uint y = draw_buffer[ draw_mesh_task_indirect_cmd_i + kGroupCountY ];
    const uint z = draw_buffer[ draw_mesh_task_indirect_cmd_i + kGroupCountZ ];
    if (x > pc.max_workgroup_count_x) {
        GpuavLogError2(kErrorGroupGpuPreDraw, kErrorSubCodePreDrawGroupCountX, x, draw_i);
    }
    if (y > pc.max_workgroup_count_y) {
        GpuavLogError2(kErrorGroupGpuPreDraw, kErrorSubCodePreDrawGroupCountY, y, draw_i);
    } 
    if (z > pc.max_workgroup_count_z) {
        GpuavLogError2(kErrorGroupGpuPreDraw, kErrorSubCodePreDrawGroupCountZ, z, draw_i);
    }

    const uint total = x * y * z;
    if (total > pc.max_workgroup_total_count) {
        GpuavLogError2(kErrorGroupGpuPreDraw, kErrorSubCodePreDrawGroupCountTotal, total, draw_i);
    }
}
